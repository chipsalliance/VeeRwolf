# JTAG adapter configuration (on-board USB-JTAG adapter on Digilent Nexys A7)
interface ftdi
ftdi_device_desc "Digilent USB Device"
ftdi_vid_pid 0x0403 0x6010
ftdi_channel 0
ftdi_layout_init 0x0088 0x008b
reset_config none
adapter_khz 10000

transport select jtag

# Configure JTAG chain and the target processor
set _CHIPNAME riscv

jtag newtap $_CHIPNAME cpu -irlen 6 -expected-id 0x03631093 -ignore-version
set _TARGETNAME $_CHIPNAME.cpu
target create $_TARGETNAME riscv -chain-position $_TARGETNAME

# No MMU on SweRV (do not attempt virt2phys address translation)
riscv set_enable_virt2phys off

# Configure memory access method
# Utilize "abstract access" to allow to reach SweRV's ICCM, DCCM and PIC core-local memories.
# Note: Requires SweRV EH1 1.8+ and recent riscv-openocd (commit 22d771d20 from Sep 14, 2020, or newer).
riscv set_mem_access abstract

# Alternate memory access configuration - via "system bus"
# Caution: ICCM, DCCM and PIC cannot be reached.
# riscv set_mem_access sysbus

# Custom numbers for RISC-V Debug JTAG registers 
# (needed due to JTAG tunneling via Xilinx BSCAN cell)
riscv set_ir idcode 0x9
riscv set_ir dmi 0x22
riscv set_ir dtmcs 0x23

# Expose custom SweRV's CSR dmst (csr1988)
riscv expose_csrs 1988

# Custom event hooks to flush SweRV ICACHE prior to step/resume
proc swerv_eh1_execute_fence {} {
    # Execute fence + fence.i via "dmst" register
    reg csr1988 0x3
}

$_TARGETNAME configure -event resume-start {
    swerv_eh1_execute_fence
}

$_TARGETNAME configure -event step-start {
    swerv_eh1_execute_fence
}

# Conclude OpenOCD configuration
init

# Halt the target
halt
